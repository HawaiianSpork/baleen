plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'org.jmailen.kotlinter' version '1.12.0'
}

apply plugin: 'idea'

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testCompile 'org.assertj:assertj-core:3.9.1'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    test {
        useJUnitPlatform()
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives sourceJar
    }

    signing {
        required { gradle.taskGraph.hasTask("publishMavenJavaPublicationToMavenRepository") }
        sign configurations.archives
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                customizePom(pom)

                from components.java

                // create the sign pom artifact
                pom.withXml {
                    def pomFile = file("${project.buildDir}/generated-pom.xml")
                    writeTo(pomFile)
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }


                artifact(sourceJar) {
                    classifier = 'sources'
                }

                // create the signed artifacts
                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    model {
        tasks.generatePomFileForMavenJavaPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        tasks.publishMavenJavaPublicationToMavenLocal {
            dependsOn project.tasks.signArchives
        }
        tasks.publishMavenJavaPublicationToMavenRepository {
            dependsOn project.tasks.signArchives
        }
    }
}


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Library for Validating Legacy Data'
            name 'Baleen'
            url 'https://github.com/ShopRunner/baleen'
            organization {
                name 'com.shoprunner'
                url 'https://github.com/ShopRunner'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/ShopRunner/baleen/issues'
            }
            licenses {
                license {
                    name 'BSD 3-Clause "New" or "Revised" License'
                    url 'https://github.com/ShopRunner/baleen/blob/master/LICENSE.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/ShopRunner/baleen'
                connection 'scm:git:git://github.com/ShopRunner/baleen.git'
                developerConnection 'scm:git:ssh://git@github.com:ShopRunner/baleen.git'
            }
            developers {
                developer {
                    name 'Shoprunner'
                }
            }
        }
    }
}